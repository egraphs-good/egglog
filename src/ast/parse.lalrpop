use crate::ast::*;
use crate::Symbol;
use crate::Schedule;
use ordered_float::OrderedFloat;

grammar;

extern {
    type Error = String;
}

// lexer
match {
    r"\s*" => { }, // skip whitespace
    r";[^\n\r]*[\n\r]*" => { }, // skip ; comments
    _
}

pub Program: Vec<Command> = { (Command)* => <> }

List<T>: Vec<T> = { 
    "(" <T*> ")" => <>,
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Command: Command = {
    "(" "datatype" <name:Ident> <variants:(Variant)*> ")" => Command::Datatype { <> },
    "(" "sort" <name:Ident> "(" <head:Ident> <tail:(Expr)*> ")" ")" => Command::Sort (name, Some((head, tail))),
    "(" "sort" <name:Ident> ")" => Command::Sort (name, None),
    "(" "function" <name:Ident> <schema:Schema> <cost:Cost>
        <merge_action:(":on_merge" <List<Action>>)?>
        <merge:(":merge" <Expr>)?> <default:(":default" <Expr>)?> ")" => {
        Command::Function(FunctionDecl { name, schema, merge, merge_action: merge_action.unwrap_or_default(), default, cost })
    },
    "(" "relation" <name:Ident> <types:List<Type>> ")" => Command::Function(FunctionDecl::relation(name, types)),
    "(" "ruleset" <name:Ident> ")" => Command::AddRuleset(name),
    "(" "rule" <body:List<Fact>> <head:List<Action>> <ruleset:(":ruleset" <Ident>)?> ")" => Command::Rule(ruleset.unwrap_or("".into()), Rule { head, body }),
    "(" "rewrite" <lhs:Expr> <rhs:Expr>
        <conditions:(":when" <List<Fact>>)?>
        <ruleset:(":ruleset" <Ident>)?>
    ")" => Command::Rewrite(ruleset.unwrap_or("".into()), Rewrite { lhs, rhs, conditions: conditions.unwrap_or_default() }),
    "(" "birewrite" <lhs:Expr> <rhs:Expr>
        <conditions:(":when" <List<Fact>>)?>
        <ruleset:(":ruleset" <Ident>)?>
    ")" => Command::BiRewrite(ruleset.unwrap_or("".into()), Rewrite { lhs, rhs, conditions: conditions.unwrap_or_default() }),
    "(" "define" <name:Ident> <expr:Expr> <cost:Cost> ")" => Command::Define { name, expr, cost },
    <NonLetAction> => Command::Action(<>),
    "(" "run" <limit:UNum>  <until:(":until" <Fact>)?> ")" => Command::Run(RunConfig { ruleset: "".into(), limit, until }),
    "(" "run" <ruleset: Ident> <limit:UNum> <until:(":until" <Fact>)?> ")" => Command::Run(RunConfig { ruleset, limit, until }),
    "(" "simplify"  <limit:UNum> <expr:Expr> <until:(":until" <Fact>)?> ")" 
        => Command::Simplify { expr, config : RunConfig { ruleset: "".into(), limit, until } },
    "(" "add-ruleset" <name:Ident> ")" => Command::AddRuleset(name),
    "(" "run-schedule" <Schedule*> ")" => Command::RunSchedule(Schedule::Sequence(<>)),
    "(" "calc" "(" <idents:IdentSort*> ")" <exprs:Expr+> ")" => Command::Calc(idents, exprs),
    "(" "extract" <variants:(":variants" <UNum>)?> <e:Expr> ")" => Command::Extract { e, variants: variants.unwrap_or(0) },
    "(" "check" <Fact> ")" => Command::Check(<>),
    "(" "clear" ")" => Command::Clear,
    "(" "query" <List<Fact>> ")" => Command::Query(<>),
    "(" "push" <UNum?> ")" => Command::Push(<>.unwrap_or(1)),
    "(" "pop" <UNum?> ")" => Command::Pop(<>.unwrap_or(1)),
    "(" "print" <sym:Ident> <n:UNum?> ")" => Command::Print(sym, n.unwrap_or(10)),
    "(" "print-size" <sym:Ident> ")" => Command::PrintSize(sym),
    "(" "input" <name:Ident> <file:String> ")" => Command::Input { name, file },
    "(" "output" <file:String> <exprs:Expr+> ")" => Command::Output { file, exprs },
    "(" "fail" <Command> ")" => Command::Fail(Box::new(<>)),
    "(" "include" <file:String> ")" => Command::Include(file),
}

Schedule: Schedule = {
    "(" "saturate" <Schedule*> ")" => Schedule::Saturate(Box::new(Schedule::Sequence(<>))),
    "(" "seq" <Schedule*> ")" => Schedule::Sequence(<>),
    "(" "repeat" <limit:UNum> <scheds:Schedule*> ")" => Schedule::Repeat(limit, Box::new(Schedule::Sequence(scheds))),
    <sym:Ident> => Schedule::Ruleset(sym),
}

Cost: Option<usize> = {
    ":cost" <UNum> => Some(<>),
    => None,
}

NonLetAction: Action = {
    "(" "set" "(" <f: Ident> <args:Expr*> ")" <v:Expr> ")" => Action::Set ( f, args, v ),
    "(" "delete" "(" <f: Ident> <args:Expr*> ")" ")" => Action::Delete ( f, args),
    "(" "union" <e1:Expr> <e2:Expr> ")" => Action::Union(<>),
    "(" "panic" <msg:String> ")" => Action::Panic(msg),
    <e:CallExpr> => Action::Expr(e),
}

Action: Action = {
    "(" "let" <name:Ident> <expr:Expr> ")" => Action::Let(name, expr),
    <NonLetAction> => <>,
}

Name: Symbol = { "[" <Ident> "]" => <> }

Fact: Fact = {
    "(" "=" <mut es:Expr+> <e:Expr> ")" => {
        es.push(e);
        Fact::Eq(es)
    },
    <Expr> => Fact::Fact(<>),
}

Schema: Schema = {
    <types:List<Type>> <output:Type> => Schema { input: types, output }
}

Expr: Expr = {
    <Literal> => Expr::Lit(<>),
    <Ident> => Expr::Var(<>),
    <CallExpr> => <>,
};

Literal: Literal = {
    // "(" ")" => Literal::Unit, // shouldn't need unit literals for now
    <Num> => Literal::Int(<>),
    <F64> => Literal::F64(<>),
    <SymString> => Literal::String(<>),
}

CallExpr: Expr = {
    "(" <head:Ident> <tail:(Expr)*> ")" => Expr::Call(head, tail),
}

ExprList: Vec<Expr> = { "(" <sexps:(Expr)*> ")" => sexps }

Variant: Variant = {
    "(" <name:Ident> <types:(Type)*> <cost:Cost> ")" => Variant { <> },
}

Type: Symbol = <Ident>;

IdentSort: IdentSort = "(" <ident:Ident> <sort:Type> ")" => IdentSort { ident, sort };
Num: i64 = <s:r"(-)?[0-9]+"> => s.parse().unwrap();
UNum: usize = {
    <Num> => <>.try_into().unwrap(),
}
F64: OrderedFloat<f64> = <s:r"(-)?[0-9]+\.[0-9]+(e(-)?[0-9]+)?"> => OrderedFloat::<f64>(s.parse().unwrap());
Ident: Symbol = <s:r"([[:alpha:]][\w-]*)|([-+*/!=<>&|^/%]+)"> => s.parse().unwrap();
SymString: Symbol = <String> => Symbol::from(<>);
String: String = <r#"("[^"]*")+"#> => {
    let string: &str = <>;
    let first_last_off: &str = &string[1..string.len() - 1];
    first_last_off.replace("\"\"", "\"")
};
